apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/utilities.gradle"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.srikandi.android"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    def configProp = loadCustomProperties("$rootDir/config.properties")
    println(configProp)

    flavorDimensions "countries"


    /**
     * Define product flavor for different countries.
     * minSdk, targetSdk, versionCode, and versionName will be follows defaultConfig.
     * Add variant variables related of application config here.
     * E.g : HOST_URL
     */
    productFlavors {
        prod_en {
            dimension "countries"
            applicationId "com.srikandi.android.en"
            buildConfigField "String", "HOST_URL", configProp["HOST_URL_PROD_EN"]
        }

        prod_id {
            dimension "countries"
            applicationId "com.srikandi.android.id"
            buildConfigField "String", "HOST_URL", configProp["HOST_URL_PROD_ID"]
        }

        stg_en {
            dimension "countries"
            applicationId "com.srikandi.android.en"
            buildConfigField "String", "HOST_URL", configProp["HOST_URL_STG_EN"]
        }

        stg_id {
            dimension "countries"
            applicationId "com.srikandi.android.id"
            buildConfigField "String", "HOST_URL", configProp["HOST_URL_STG_ID"]
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation core_libraries.kotlin_stdlib
    implementation core_libraries.kotlin_core
    implementation core_libraries.appcompat
    implementation core_libraries.constraintlayout
    testImplementation core_libraries.junit
    androidTestImplementation core_libraries.junit_ext
    androidTestImplementation core_libraries.espresso

}