apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/utilities.gradle"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.srikandi.android"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    def configProp = loadCustomProperties("$rootDir/config.properties")
    println(configProp)

    flavorDimensions "countries", "environment"

    productFlavors {
        /**
         * Define product flavor for different countries.
         * minSdk, targetSdk, versionCode, and versionName will be follows defaultConfig.
         * Define variant related to countries
         * E.g : application name, resources
         */
        en {
            dimension "countries"
            applicationId "com.srikandi.android.en"
            resValue "string", "app_name", "Demo App"
        }

        idn {
            dimension "countries"
            applicationId "com.srikandi.android.id"
            resValue "string", "app_name", "Demo App 1"
        }


        /**
         * Define product flavor for different environment.
         * Add variant related build environment.
         * E.g : HOST_URL
         */
        prod {
            dimension "environment"
            buildConfigField "String", "HOST_URL", configProp["HOST_URL_PROD"]
        }

        stg {
            dimension "environment"
            buildConfigField "String", "HOST_URL", configProp["HOST_URL_STG"]
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation core_libraries.kotlin_stdlib
    implementation core_libraries.kotlin_core
    implementation core_libraries.appcompat
    implementation core_libraries.constraintlayout
    testImplementation core_libraries.junit
    androidTestImplementation core_libraries.junit_ext
    androidTestImplementation core_libraries.espresso

}